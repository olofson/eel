/////////////////////////////////////////////
// beep.eel - Simple Audio Test
// Copyright 2006, 2017, 2019 David Olofson
/////////////////////////////////////////////

import SDL, math, oscilloscope as oscilloscope;

constant FS = 44100;	// Output sample rate (Hz)
constant BUF = 2048;	// Output buffer size (sample frames)
constant FIFO = 8192;	// PlayAudio() FIFO buffer size (sample frames)

export function main<args>
{
	local osc = oscilloscope.Open("SDL Audio Test");

	OpenAudio(FS, BUF, FIFO);
	local ph = 0;
	local dph = 2. * PI * 440 / FS;
	while true
	mainloop:
	{
		while true
		{
			local ev = PollEvent();
			if not ev
				break;
			switch ev.type
			  case KEYUP
				if ev.sym == KESCAPE
					break mainloop;
			  case MOUSEBUTTONUP, QUIT
				break mainloop;
		}
		while AudioSpace()
		{
			local s = sin(ph);
			ph += dph;
			PlayAudio(s, s);
			osc:Feed(s);
		}
		Delay(10);
	}

	CloseAudio();
	return 0;
}
